buildscript {
    apply from: 'https://raw.githubusercontent.com/FINTprosjektet/fint-buildscripts/v1.0.12/dependencies.gradle'
    ext {
        springBootVersion = springBootVersion
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.15.0'
    id 'com.github.kt3k.coveralls' version '2.8.1'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven'
apply plugin: 'jacoco'

apply from: 'https://raw.githubusercontent.com/FINTlibs/fint-buildscripts/v1.0.12/version.gradle'
jar {
    version = createVersion()
    baseName = 'fint-provider'
}

import org.apache.tools.ant.filters.ReplaceTokens
afterEvaluate {
    configure(processResources) {
        filesMatching('**/*.yml') {
            filter(ReplaceTokens, tokens: [version: jar.version])
        }
    }
}

sourceCompatibility = 1.8

repositories {
    maven {
        url "http://dl.bintray.com/fint/maven"
    }
    mavenLocal()
    jcenter()
}


dependencies {
    compile('no.fint:fint-audit-mongo-plugin:0.0.13')
    compile('no.fint:fint-events:0.1.26')
    compile('no.fint:fint-event-model:0.0.23')
    compile('no.fint:fint-springfox-extension:0.0.1')

    compile('org.projectlombok:lombok')
    compile("com.github.springfox.loader:springfox-loader:${springfoxLoaderVersion}")
    compile('com.github.fakemongo:fongo:2.1.0')
    compile('com.jayway.jsonpath:json-path:2.2.0')
    compile('org.reflections:reflections:0.9.11')

    compile('org.springframework.boot:spring-boot-starter-web')
    runtime('org.springframework.boot:spring-boot-devtools')

    testCompile('no.fint:fint-test-utils:0.0.3')
    testCompile('cglib:cglib-nodep:3.2.5')
    testCompile("org.spockframework:spock-spring:${spockSpringVersion}")
    testCompile("org.spockframework:spock-core:${spockSpringVersion}")
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleVersion
}

bootRun {
    systemProperties System.properties
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

apply from: 'https://raw.githubusercontent.com/FINTlibs/fint-buildscripts/v1.0.12/dependencyReport.gradle'