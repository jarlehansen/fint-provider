buildscript {
    apply from: 'https://raw.githubusercontent.com/FINTprosjektet/fint-buildscripts/v1.8.0/dependencies.gradle'
    ext {
        springBootVersion = springBootVersion
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.25.0'
    id 'com.github.kt3k.coveralls' version '2.8.4'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven'
apply plugin: 'jacoco'

import org.apache.tools.ant.filters.ReplaceTokens
afterEvaluate {
    configure(processResources) {
        filesMatching('**/*.yml') {
            filter(ReplaceTokens, tokens: [version: jar.version])
        }
    }
}

sourceCompatibility = 1.8

repositories {
    maven {
        url 'https://repo.fintlabs.no/releases'
    }
    mavenLocal()
    jcenter()
}


dependencies {
    compile('no.fint:fint-sse:1.4.1') {
        exclude group: 'org.springframework.security.oauth', module: 'spring-security-oauth2'
    }
    compile('org.glassfish.jersey.core:jersey-client:2.29.1')
    compile('org.glassfish.jersey.core:jersey-common:2.29.1')
    
    compile('no.fint:fint-audit-azure-eventhub-plugin:1.0.0')
    compile('io.projectreactor:reactor-core:3.3.2.RELEASE')
    
    compile('no.fint:fint-events:3.0.0-alpha-1')
    compile('no.fint:fint-event-model:3.0.1')
    compile('no.fint:fint-springfox-extension:0.0.2')
    compile('no.fint:fint-hazelcast:1.2.0')
    compile('com.hazelcast:hazelcast:3.11.1')
    compile('com.hazelcast:hazelcast-kubernetes:1.3.1')

    compile("org.projectlombok:lombok:${lombokVersion}")
    compile("com.github.springfox.loader:springfox-loader:${springfoxLoaderVersion}")
    compile('com.github.fakemongo:fongo:2.1.1')
    compile('com.jayway.jsonpath:json-path:2.4.0')
    compile('org.reflections:reflections:0.9.11')
    compile('org.apache.commons:commons-lang3:3.9')
    compile('org.jooq:jool-java-8:0.9.14')

    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.apache.tomcat.embed:tomcat-embed-core:8.5.45')
    runtime('org.springframework.boot:spring-boot-starter-actuator')

    testCompile('no.fint:fint-test-utils:0.0.6')
    testCompile("cglib:cglib-nodep:${cglibVersion}")
    testCompile("org.spockframework:spock-spring:${spockSpringVersion}")
    testCompile("org.spockframework:spock-core:${spockSpringVersion}")
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

task copyExternalDependencies(type: Copy) {
    from configurations.runtime
    into "$buildDir/deps/external"
    exclude '**/fint-*.jar'
}

task copyFintDependencies(type: Copy) {
    from configurations.runtime
    into "$buildDir/deps/fint"
    include '**/fint-*.jar'
}

build.dependsOn(copyExternalDependencies)
build.dependsOn(copyFintDependencies)

jar {
    baseName = 'fint-provider'
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Main-Class": "no.fint.provider.events.Application",
                "Specification-Version": "${version}",
                "Implementation-Version": "${version}",
                "Class-Path": configurations.runtime.collect { it.getName() }.join(' ')
        )
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = gradleVersion
}

bootRepackage.enabled = false

bootRun {
    systemProperties System.properties
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

apply from: 'https://raw.githubusercontent.com/FINTlibs/fint-buildscripts/v1.8.0/dependencyReport.gradle'
